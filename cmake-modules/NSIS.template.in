;==============================================================================================
; Template to generate the Windows installer for LenMus. Unicode version
;
; NSIS v3.04 script for generating the exe installer and uninstaller
;
;--------------------------------------------------------------------------------------
;    LenMus Phonascus: The teacher of music
;    Copyright (c) 2002-2020 Cecilio Salmeron
;
;    This program is free software; you can redistribute it and/or modify it under the 
;    terms of the GNU General Public License as published by the Free Software Foundation;
;    either version 3 of the License, or (at your option) any later version.
;
;    This program is distributed in the hope that it will be useful, but WITHOUT ANY 
;    WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
;    PARTICULAR PURPOSE.  See the GNU General Public License for more details.
;
;    You should have received a copy of the GNU General Public License
;    along with this program.  If not, see <http://www.gnu.org/licenses/>.
;
;    For any comment, suggestion or feature request, please contact the manager of 
;    the project at cecilios@users.sourceforge.net
;
;==============================================================================================


;perform a CRC on the installer before allowing an install to make sure the installer
;is not corrupted
  CRCCheck on
  
;build Unicode installer
  Unicode true
  
;set the compression algorithm to use
  SetCompressor /SOLID lzma

;use the new XP controls style when running on Windows XP
  XPStyle on
  
;declare installer as "DPI aware" so that it is not scaled by Windows
  ManifestDPIAware true 
    
;set Execution Level to Admin
  RequestExecutionLevel admin

;values set in CMake/CPack script
;Aware: only variables starting by 'CPACK_" are used by CPack to configure this template 
  !define APP_VERSION "@CPACK_PACKAGE_VERSION@"                         ;version. e.g.: "5.4.2" 
  !define LENMUS_ROOT "@CPACK_LENMUS_ROOT@"                             ;path to lenmus sources
  !define INSTALLER_NAME "@CPACK_OUTPUT_FILE_NAME@"                     ;name of installer. e.g: "lenmus-5.4.2-win64.exe"
  !define INSTALLER_BUILD_FOLDER "@CPACK_LENMUS_BUILD_INSTALLER_PATH@"  ;path for building the installer
  !define INST_DIR "@CPACK_TEMPORARY_DIRECTORY@"

;fix char separator in install path
  !searchreplace INSTALL_PATH "@CPACK_LENMUS_INSTALL_PATH@" "/" "\"

    
;some helper defines and variables
  !define APP_NAME "LenMus Phonascus ${APP_VERSION}"
  !define APP_HOME_PAGE "http://www.lenmus.org/"
  !define LENMUS_EXE "lenmus.exe"                                   ;name of exec. e.g.: "lenmus_5.4.1.exe"
  !define LENMUS_START_NAME "LenMus ${APP_VERSION}"               ;name in Windows Start Menu. e.g. "LenMus 5.5.0"
  !define LENMUS_NSIS_RESOURCES "${LENMUS_ROOT}\installer\msw"
  !define LENMUS_NSIS_LOCALE "${LENMUS_ROOT}\installer\msw\locale"

;product name displayed by the installer
  Name "lenmus v${APP_VERSION}"     

;Specify path and name for uninstaller
  !define UNINSTALLER_NAME "uninstall-${INSTALLER_NAME}"
  OutFile "${INSTALLER_NAME}"

;define the default installation directory and registry key
  !define REGISTRY_UNINSTALL "LenMus_${APP_VERSION}"
  !define REGISTRY_DATA "LenMus\Phonascus\${APP_VERSION}"

  InstallDir "${INSTALL_PATH}-${APP_VERSION}"
  InstallDirRegKey HKCU "Software\${REGISTRY_DATA}" ""


;support for Modern UI
  !include "MUI2.nsh"

;support for GetParent
  !include "FileFunc.nsh"
  !insertmacro un.GetParent
  
;support to remove files and subdirs in one step
  !include ${LENMUS_NSIS_RESOURCES}\RemoveFilesAndSubDirs.nsh

;other includes:
  !include "x64.nsh"            ;support for x64 helpers
  !include nsDialogs.nsh        ;support for changing MUI images

;configuration settings for the MUI interface: options, icons, bitmaps, colors, etc.
  ;show a warning when the user cancels the install
    !define MUI_ABORTWARNING
  ;icons to associate to the resulting installer and uninstaller
    !define MUI_ICON "${LENMUS_NSIS_RESOURCES}\lenmus.ico"
    !define MUI_UNICON "${LENMUS_NSIS_RESOURCES}\win-uninstall.ico"

!define MUI_HEADERIMAGE
;!define MUI_HEADERIMAGE_BITMAP "${LENMUS_NSIS_RESOURCES}\BannerTop-96.bmp"
;!define MUI_WELCOMEFINISHPAGE_BITMAP "${LENMUS_NSIS_RESOURCES}\BannerLeft-96.bmp"
!define MUI_COMPONENTSPAGE_SMALLDESC        ;descriptions at bottom
      

;vars to create links on "Start Menu" folder
  Var MUI_TEMP
  Var STARTMENU_FOLDER
  
;var to store root install directory in uninstall section
  Var LENMUS_DIR


;MUI: install pages to show
  ;first of all determine DPI
    !define MUI_PAGE_CUSTOMFUNCTION_SHOW DetermineDpi
  ;welcome page
    !insertmacro MUI_PAGE_WELCOME

  ;show and accept licence
    !insertmacro MUI_PAGE_LICENSE $(license)

;  ;choose installation directory
;    !insertmacro MUI_PAGE_DIRECTORY

;  ;select optional components
;    !define MUI_COMPONENTSPAGE_TEXT_COMPLIST $(MSG_OptionalComponents)
;    !insertmacro MUI_PAGE_COMPONENTS

;  ;ask about creating links on Start Menu
;    !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU"
;    !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\${REGISTRY_DATA}"
;    !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "StartMenuFolder"
;    !define MUI_STARTMENUPAGE_DEFAULTFOLDER $(SM_PRODUCT_GROUP)
;    !insertmacro MUI_PAGE_STARTMENU Application $STARTMENU_FOLDER

  ;instalation page
    !insertmacro MUI_PAGE_INSTFILES

  ;finish page: run installed program?
    !define MUI_FINISHPAGE_RUN "$INSTDIR\bin\${LENMUS_EXE}"
    !define MUI_PAGE_CUSTOMFUNCTION_SHOW DetermineDpi
    !insertmacro MUI_PAGE_FINISH


;MUI: uninstall Pages
;  ;ask for confirmation to uninstall
;  !insertmacro MUI_UNPAGE_CONFIRM
  
  ;proceed to uninstall
  !insertmacro MUI_UNPAGE_INSTFILES



;language selection dialog settings
  ;Remember the installer language
  !define MUI_LANGDLL_REGISTRY_ROOT "HKCU"
  !define MUI_LANGDLL_REGISTRY_KEY "Software\${REGISTRY_DATA}"
  !define MUI_LANGDLL_REGISTRY_VALUENAME "Installer Language"


  ;available languages
  !define MUI_LANGDLL_ALLLANGUAGES
  !addincludedir "${INSTALLER_BUILD_FOLDER}"
  !include "mui_languages.nsh"

  ;reserve files for languages
    ;These files should be inserted before other files in the data block
    ;Keep these lines before any File command
    !insertmacro MUI_RESERVEFILE_LANGDLL


;variable to retry installation when error found
  var "STEP"


;instruct installer and uninstaller to show install/uninstall log to the user
  ShowInstDetails show                   ;show install log 
  ShowUnInstDetails show                 ;show uninstall log




;---------------------------------------------------------------------------------------------------
;languaje files to support different languages during installation
;---------------------------------------------------------------------------------------------------
  !addincludedir "${INSTALLER_BUILD_FOLDER}"
  !include "mui_locale.nsh"


; *********************************************************************
; Install Sections
; *********************************************************************

;Specify the requested execution level for Windows
  RequestExecutionLevel admin
 

;Start installation
Function .onInit
;	${If} ${RunningX64}
;        Goto RunX64
;    ${Else}
;      MessageBox MB_OK|MB_ICONEXCLAMATION $(MSG_Not_x64)
;      Abort "$(MSG_ABORT)"
;    ${EndIf}

;RunX64:
    ;extract bitmaps as they can not be used directly from installer
    InitPluginsDir
    
    File /oname=$PLUGINSDIR\BannerTop-96.bmp ${LENMUS_NSIS_RESOURCES}\BannerTop-96.bmp
    File /oname=$PLUGINSDIR\BannerTop-120.bmp ${LENMUS_NSIS_RESOURCES}\BannerTop-120.bmp
    File /oname=$PLUGINSDIR\BannerTop-144.bmp ${LENMUS_NSIS_RESOURCES}\BannerTop-144.bmp
    File /oname=$PLUGINSDIR\BannerTop-192.bmp ${LENMUS_NSIS_RESOURCES}\BannerTop-192.bmp

    File /oname=$PLUGINSDIR\BannerLeft-96.bmp ${LENMUS_NSIS_RESOURCES}\BannerLeft-96.bmp
    File /oname=$PLUGINSDIR\BannerLeft-120.bmp ${LENMUS_NSIS_RESOURCES}\BannerLeft-120.bmp
    File /oname=$PLUGINSDIR\BannerLeft-144.bmp ${LENMUS_NSIS_RESOURCES}\BannerLeft-144.bmp
    File /oname=$PLUGINSDIR\BannerLeft-192.bmp ${LENMUS_NSIS_RESOURCES}\BannerLeft-192.bmp
   
    ;enable access to 64-bit portion of registry
    SetRegView 64
    
    ;Check if the user is running multiple instances of the installer
    System::Call 'kernel32::CreateMutexA(i 0, i 0, t "LenMusMutex") i .r1 ?e'
    Pop $R0
    StrCmp $R0 0 +3
      MessageBox MB_OK|MB_ICONEXCLAMATION $(MSG_Running)
      Abort "$(MSG_ABORT)"

    ;Show installer language selection page
    !insertmacro MUI_LANGDLL_DISPLAY
    
FunctionEnd

;------------
;function to determine bitmaps dpi to use (in $0)
;------------
Function DetermineDpi
    System::Call USER32::GetDpiForSystem()i.r0 
    ${If} $0 <= 0 
        System::Call USER32::GetDC(i0)i.r1 
        System::Call GDI32::GetDeviceCaps(ir1,i88)i.r0 
        System::Call USER32::ReleaseDC(i0,ir1) 
    ${EndIf} 

    ${If} $0 > 144
        StrCpy $0 192
    ${ElseIf} $0 > 120
        StrCpy $0 144
    ${ElseIf} $0 > 96
        StrCpy $0 120
    ${Else}
        StrCpy $0 96
    ${EndIf} 

    ${NSD_SetImage} $mui.WelcomePage.Image $PLUGINSDIR\BannerLeft-$0.bmp $mui.WelcomePage.Image.Bitmap
    ${NSD_SetImage} $mui.FinishPage.Image $PLUGINSDIR\BannerLeft-$0.bmp $mui.FinishPage.Image.Bitmap
    SetBrandingImage /IMGID=1046 "$PLUGINSDIR\BannerTop-$0.bmp"
    
;    MessageBox MB_OK 'DPI is "$0"'
    
FunctionEnd

;------------
;function to write an string into a file
; Usage:
;   Push "hello$\r$\n" ;text to write to file 
;   Push "$INSTDIR\log.txt" ;file to write to 
;   Call WriteToFile
;------------
Function WriteToFile
 Exch $0 ;file to write to
 Exch
 Exch $1 ;text to write
 
  FileOpen $0 $0 a #open file
   FileSeek $0 0 END #go to end
   FileWrite $0 $1 #write to file
  FileClose $0
 
 Pop $1
 Pop $0
FunctionEnd


;Install all the mandatory components
Section  "-" "MainSection"

  ; vars initialization
  StrCpy "$STEP" "Nil" 

  ;If a previous version exits delete old installed files
  ;-----------------------------------------------------------------------------------
  !insertmacro RemoveFilesAndSubDirs "$INSTDIR\bin"
  !insertmacro RemoveFilesAndSubDirs "$INSTDIR\docs"
  !insertmacro RemoveFilesAndSubDirs "$INSTDIR\locale"
  !insertmacro RemoveFilesAndSubDirs "$INSTDIR\res"
  !insertmacro RemoveFilesAndSubDirs "$INSTDIR\samples"
  !insertmacro RemoveFilesAndSubDirs "$INSTDIR\share"
  !insertmacro RemoveFilesAndSubDirs "$INSTDIR\templates"
  !insertmacro RemoveFilesAndSubDirs "$INSTDIR\xrc"


  ;install application files
  ;-----------------------------------------------------------------------------------
  CopyFiles:
     ClearErrors
     SetOverWrite try
     
     ;Copy the entire tree produced by the CMake INSTALL target.
     SetOutPath "$INSTDIR"
     @CPACK_NSIS_EXTRA_PREINSTALL_COMMANDS@
     @CPACK_NSIS_FULL_INSTALL@
      
     ;install required dlls
     SetOutPath "$INSTDIR\bin"
     File "${LENMUS_NSIS_RESOURCES}\*.dll"

      IfErrors +1 EndCopyFiles
        StrCmp $STEP "ErrorCopyingFiles" "Error_CopyFiles"
        StrCpy "$STEP" "ErrorCopyingFiles" 
      Goto CopyFiles
        Error_CopyFiles:
          MessageBox MB_RETRYCANCEL|MB_ICONEXCLAMATION $(ERROR_CopyFiles) IDRETRY CopyFiles
          MessageBox MB_YESNO|MB_ICONQUESTION $(MSG_CONTINUE) IDYES +2
      Abort "$(MSG_ABORT)"
     EndCopyFiles:

  ; Save install options
  ;-----------------------------------------------------------------------------------
     SetOutPath "$INSTDIR\bin"
     Push "$(OTHER_LangName)"                   ;text to write to file 
     Push "$INSTDIR\bin\config_ini.txt"         ;file to write to 
     Call WriteToFile

  ;Write key to specify that LenMus is DPI aware
    WriteRegStr HKLM "Software\Microsoft\Windows NT\CurrentVersion\AppCompatFlags\Layers" "$INSTDIR\bin\${LENMUS_EXE}" "~ HIGHDPIAWARE"

  ;create entries in Start Menu folder
  ;-----------------------------------------------------------------------------------
;  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    CreateDirectory "$SMPROGRAMS\$STARTMENU_FOLDER"
;    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\$BMP_DPI.lnk" "$INSTDIR\bin\${LENMUS_EXE}" "" "$INSTDIR\res\icons\lenmus.ico" 0
    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\${LENMUS_START_NAME}.lnk" "$INSTDIR\bin\${LENMUS_EXE}" "" "$INSTDIR\res\icons\lenmus.ico" 0
;   CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\$(SHORTCUT_NAME_EXEC).lnk" "$INSTDIR\bin\${LENMUS_EXE}" "" "$INSTDIR\res\icons\lenmus.ico" 0;
;    CreateShortCut "$SMPROGRAMS\$STARTMENU_FOLDER\$(SHORTCUT_NAME_UNINSTALL).lnk" "$INSTDIR\bin\${UNINSTALLER_NAME}"
;  !insertmacro MUI_STARTMENU_WRITE_END

    ;write uninstaller
    WriteUninstaller "$INSTDIR\bin\${UNINSTALLER_NAME}"
    
    
; Add uninstall information to Add/Remove Programs so that lenmus program 
; will be included on the "Add/Remove Programs" folder of the Control Pannel 
; http://msdn.microsoft.com/en-us/library/aa372105%28VS.85%29.aspx
;-----------------------------------------------------------------------------------------------

    ; mandatory keys for un-installing
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${REGISTRY_UNINSTALL}" "DisplayName" "$(^Name)"
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${REGISTRY_UNINSTALL}" "UninstallString" "$INSTDIR\bin\${UNINSTALLER_NAME}"

    ; optional information
    
    ;NoModify (DWORD) - 1 if uninstaller has no option to modify the installed application
    ;NoRepair (DWORD) - 1 if the uninstaller has no option to repair the installation
    WriteRegDWord HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${REGISTRY_UNINSTALL}" "NoModifiy" 1
    WriteRegDWord HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${REGISTRY_UNINSTALL}" "NoRepair" 1

    ; additional info for updater
    WriteRegStr HKCU "Software\${REGISTRY_DATA}" "InstallRoot" "$INSTDIR"

SectionEnd

;===============================================================================================================
; Installation of optional components
;===============================================================================================================

;-----------------------------------------------------------------------------------------------
; shortcut icon on desktop
;-----------------------------------------------------------------------------------------------
Section $(TITLE_CreateIcon) CreateIcon

  CreateIcon:
     ClearErrors
     CreateShortCut "$DESKTOP\LenMus ${APP_VERSION}.lnk" "$INSTDIR\bin\${LENMUS_EXE}" "" "$INSTDIR\res\icons\lenmus.ico" 0
     IfErrors +1 EndCreateIcon
        StrCmp $STEP "ErrorCreatingIcon" "Error_CreateIcon"
        StrCpy "$STEP" "ErrorCreatingIcon" 
      Goto CreateIcon
        Error_CreateIcon:
          MessageBox MB_RETRYCANCEL|MB_ICONEXCLAMATION $(ERROR_CreateIcon) IDRETRY CreateIcon
          MessageBox MB_YESNO|MB_ICONQUESTION $(MSG_CONTINUE) IDYES +2
      Abort "$(MSG_ABORT)"
     EndCreateIcon:

SectionEnd

;-----------------------------------------------------------------------------------------------
; sample scores 
;-----------------------------------------------------------------------------------------------
Section $(TITLE_Scores) Scores

  CopyScores:
     ClearErrors
     SetOverwrite on
     SetOutPath "$INSTDIR\scores\MusicXML"
     File "${LENMUS_ROOT}\scores\MusicXML\*.xml"
     SetOutPath "$INSTDIR\scores\samples"
     File "${LENMUS_ROOT}\scores\samples\*.lms"
     IfErrors +1 EndCopyScores
        StrCmp $STEP "ErrorCopyingScores" "Error_CopyScores"
        StrCpy "$STEP" "ErrorCopyingScores" 
      Goto CopyScores
        Error_CopyScores:
          MessageBox MB_RETRYCANCEL|MB_ICONEXCLAMATION $(ERROR_CopyScores) IDRETRY CopyScores
          MessageBox MB_YESNO|MB_ICONQUESTION $(MSG_CONTINUE) IDYES +2
          Abort "$(MSG_ABORT)"
     EndCopyScores:

SectionEnd



;assign descriptions to sections -------------------------------------------------------

  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${CreateIcon} $(DESC_CreateIcon)
    !insertmacro MUI_DESCRIPTION_TEXT ${Scores} $(DESC_Scores)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END




############################################################################################
;                                 Uninstaller                                              ;
############################################################################################

Function un.onInit
  ;first of all, show installer language selection page if language not set
    !insertmacro MUI_LANGDLL_DISPLAY
    
    SetRegView 64
FunctionEnd

/*
Function un.onInit
  FindWindow $R0 "THMNISEdit2_MainWindowClass"
  IsWindow $R0 0 +3
  MessageBox MB_ICONEXCLAMATION|MB_OK "lenmus está ejecutándose. Antes de desinstalar lenmus debe cerrarlo."
  Abort

  UserInfo::GetAccountType
  Pop $R0
  StrCmp $R0 "Admin" 0 +2
  SetShellVarContext all
FunctionEnd
*/

Section un.Install
  
  ; move to root directory and delete all folders and files
  ${un.GetParent} "$INSTDIR" $LENMUS_DIR
  !insertmacro RemoveFilesAndSubDirs "$LENMUS_DIR"
  RMDir "$LENMUS_DIR"

  ;delete link on desktop
  Delete "$DESKTOP\lenmus ${APP_VERSION}.lnk"
  
  ;delete Start Menu folder entries
  !insertmacro MUI_STARTMENU_GETFOLDER Application $MUI_TEMP
  Delete "$SMPROGRAMS\$MUI_TEMP\*.*"
  StrCpy $MUI_TEMP "$SMPROGRAMS\$MUI_TEMP"
  RMDir $MUI_TEMP

  ;delete registry keys created by the installer
  DeleteRegKey  HKCU "Software\${REGISTRY_DATA}"
  DeleteRegKey  HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${REGISTRY_UNINSTALL}"
  
  SetAutoClose false
  
SectionEnd

;End of lenmus install/uninstall script
